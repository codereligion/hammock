subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    ext.packaging = 'jar'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations {
        provided
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    tasks.withType(Compile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs = ['-Werror', '-Xlint:-processing']
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    artifactId 'hammock-' + project.name
                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://choosealicense.com/licenses/mit/'
                            distribution 'repo'
                        }
                    }
                    name 'GradleFx'
                    packaging 'jar'

                    scm {
                        url 'scm:git@github.com:whiskeysierra/hammock.git'
                        connection 'scm:git@github.com:whiskeysierra/hammock.git'
                        developerConnection 'scm:git@github.com:whiskeysierra/hammock.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'whiskeysierra'
                            name 'Willi Sch√∂nborn'
                        }
                    }
                }
            }
        }
    }

}
